Expression_for_v1 {
  Expression
    = Expression "," LogicalExpression -- comma_sequence
    | ExpressionWithoutSequence

  ExpressionWithoutSequence
    = TernaryExpression

  TernaryExpression
    = Expression "?" Expression ":" Expression -- ternary
    | LogicalExpression

  BinaryExpression
    = LogicalExpression

  LogicalExpression
    = LogicalExpression "&&" EqualityExpression -- and
    | LogicalExpression "||" EqualityExpression -- or
    | EqualityExpression

  EqualityExpression
    = EqualityExpression "=" RelationalExpression -- loose_equal
    | EqualityExpression "==" RelationalExpression -- equal
    | EqualityExpression "!=" RelationalExpression -- not_loose_equal
    | EqualityExpression "!==" RelationalExpression -- not_equal
    | RelationalExpression

  RelationalExpression
    = RelationalExpression "<" RegExMatchExpression -- lessthan
    | RelationalExpression "<=" RegExMatchExpression -- lessthan_equal
    | RelationalExpression ">" RegExMatchExpression -- greaterthan
    | RelationalExpression ">=" RegExMatchExpression -- greaterthan_equal
    | RegExMatchExpression

  RegExMatchExpression
    = RegExMatchExpression "~=" ConcatenateExpression -- regex_match
    | ConcatenateExpression

  ConcatenateExpression
    = ConcatenateExpression #(whitespace) BitwiseExpression -- space
    | ConcatenateExpression "." BitwiseExpression -- dot
    | BitwiseExpression

  BitwiseExpression
    = BitwiseExpression "|" ~"|" AdditiveExpression -- or
    | BitwiseExpression "^" AdditiveExpression -- xor
    | BitwiseExpression "&" ~"&" AdditiveExpression -- and
    | BitshiftExpression

  BitshiftExpression
    = BitshiftExpression "<<" AdditiveExpression -- left
    | BitshiftExpression ">>" AdditiveExpression -- right
    | BitshiftExpression ">>>" AdditiveExpression -- logical_right
    | AdditiveExpression

  AdditiveExpression
    = AdditiveExpression "+" MultiplicativeExpression -- addition
    | AdditiveExpression "-" MultiplicativeExpression -- subtraction
    | MultiplicativeExpression

  MultiplicativeExpression
    = MultiplicativeExpression "*" MultiplicativeExpression -- multiplication
    | MultiplicativeExpression "/" MultiplicativeExpression -- division
    | MultiplicativeExpression "//" MultiplicativeExpression -- floor_division
    | ExponentiationExpression

  ExponentiationExpression
    = ExponentiationExpression "**" MemberExpression -- power
    | UnaryExpression

  UnaryExpression
    = "++" UnaryExpression -- increment
    | "--" UnaryExpression -- decrement
    | "+" UnaryExpression -- positive
    | "-" UnaryExpression -- negative
    | "!" UnaryExpression -- not
    | "&" ~"&" UnaryExpression -- address
    | "~" UnaryExpression -- bitwise_not
    | "*" UnaryExpression -- dereference
    | PostfixUnaryExpression

  PostfixUnaryExpression
    = CallExpression "++" -- increment
    | CallExpression "--" -- decrement
    | CallExpression

  CallExpression
    = CallExpression "(" Arguments ")" -- call
    | MemberExpression
  Arguments = ListOf<ExpressionWithoutSequence, ",">

  MemberExpression
    = MemberExpression "." #(identifier) -- propertyaccess
    | MemberExpression #(whitespace* "[") Arguments "]" -- elementaccess
    | PrimaryExpression

  PrimaryExpression
    = identifier
    | literal
    | DereferenceExpressions
    | ParenthesizedExpression

  ParenthesizedExpression = "(" Expression ")"
  DereferenceExpressions = (#(identifier) | DereferenceExpression)+
  DereferenceExpression = #("%" identifier "%")

  whitespace = " " | "\t"
  lineTerminator
    = "\r\n"
    | "\n"

  identifier = identifierStart identifierPart*
  identifierStart = letter | "_" | "$" | "@" | "#"
  identifierPart = identifierStart | digit

  literal
    = stringLiteral
    | numericLiteral

  stringLiteral = "\"" doubleCharacter* "\""
  commonEscapeSequence = "``" | "`," | "`%" | "`;" | "`::" | "`r" | "`n" | "`b" | "`t" | "`v" | "`a" | "`f"
  doubleEscapeSequence = "\"\"" | commonEscapeSequence
  doubleCharacter
    = ~("\"" | "`" | lineTerminator) any
    | doubleEscapeSequence

  numericLiteral = "0" | decimalLiteral | hexIntegerLiteral
  digitWithoutZero = "1".."9"
  decimalLiteral = decimalIntegerLiteral
  decimalIntegerLiteral = digitWithoutZero digit*
  hexIntegerLiteral = "0x" hexDigit+
                    | "0X" hexDigit+
}