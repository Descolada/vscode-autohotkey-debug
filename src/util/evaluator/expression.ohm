Expression_for_v1 {
  Expression
    = TernaryExpression
    | BinaryExpression
    | MemberExpression

  PrimaryExpression
    = ParenthesizedExpression
    | CallExpression
    | UnaryExpression
    | identifier
    | literal

  ParenthesizedExpression = "(" Expression ")"
  UnaryExpression
    = "+" ~space PrimaryExpression -- positive
    | "-" ~space PrimaryExpression -- negative
    | "!" ~space PrimaryExpression -- not
    | "&" ~(space | "&") PrimaryExpression -- address
    | PrefixUnaryExpression

  PrefixUnaryExpression
    = "++" PrimaryExpression -- increment
    | "--" PrimaryExpression -- decrement
    | PostfixUnaryExpression

  PostfixUnaryExpression
    = Expression "++"
    | Expression "--"

  TernaryExpression
    = Expression "?" Expression ":" Expression

  BinaryExpression
    = ConcatenateExpression
  ConcatenateExpression
    = ConcatenateExpression #(whitespace) LogicalExpression -- concatenate
    | LogicalExpression
  LogicalExpression
    = LogicalExpression "&&" RelationalExpression -- and
    | LogicalExpression "||" RelationalExpression -- or
    | RelationalExpression
  RelationalExpression
    = RelationalExpression "<" AdditiveExpression -- lessthan
    | RelationalExpression "<=" AdditiveExpression -- lessthan_equal
    | RelationalExpression ">" AdditiveExpression -- greaterthan
    | RelationalExpression ">=" AdditiveExpression -- greaterthan_equal
    | RelationalExpression "=" AdditiveExpression -- loose_equal
    | RelationalExpression "==" AdditiveExpression -- equal
    | RelationalExpression "!=" AdditiveExpression -- not_loose_equal
    | RelationalExpression "!==" AdditiveExpression -- not_equal
    | RegExMatchExpression
  RegExMatchExpression
    = RegExMatchExpression "~=" AdditiveExpression -- regex_match
    | AdditiveExpression
  AdditiveExpression
    = AdditiveExpression "+" MultiplicativeExpression -- addition
    | AdditiveExpression "-" MultiplicativeExpression -- subtraction
    | AdditiveExpression "." MultiplicativeExpression -- concatenate
    | MultiplicativeExpression
  MultiplicativeExpression
    = MultiplicativeExpression "*" MemberExpression -- multiplication
    | MultiplicativeExpression "/" MemberExpression -- division
    | MemberExpression

  Arguments = ListOf<Expression, ",">

  MemberExpression
    = MemberExpression "." #(identifier) -- propertyaccess
    | MemberExpression #(whitespace* "[") Arguments "]" -- elementaccess
    | PrimaryExpression
  CallExpression = MemberExpression "(" Arguments ")"

  whitespace = " " | "\t"
  lineTerminator
    = "\r\n"
    | "\n"

  identifier = identifierStart identifierPart*
  identifierStart = letter | "_" | "$" | "@" | "#"
  identifierPart = identifierStart | digit

  literal
    = thisLiteral
    | stringLiteral
    | numericLiteral
    | booleanLiteral

  thisLiteral = "this"

  booleanLiteral = ("true" | "false")

  stringLiteral = "\"" doubleCharacter* "\""
  doubleEscapeSequence = "`," | "`%" | "`;" | "`::" | "`r" | "`n" | "`b" | "`t" | "`v" | "`a" | "`f"
  doubleCharacter
    = ~("\"" | "`" | lineTerminator) any
    | doubleEscapeSequence

  numericLiteral = "0" | decimalLiteral | hexIntegerLiteral
  digitWithoutZero = "1".."9"
  decimalLiteral = decimalIntegerLiteral
  decimalIntegerLiteral = digitWithoutZero digit*
  hexIntegerLiteral = "0x" hexDigit+
                    | "0X" hexDigit+
}